
#version 450

layout(local_size_x = 16, local_size_y = 16) in;
layout(set=0, binding=0, rg16f) uniform readonly image2D advectedVelocityField;
layout(set=0, binding=1) uniform writeonly image2D divergenceField;

layout(push_constant) uniform PushConstants {
  int width;
  int height;
  float dt;
  float sorOmega;
  float density;
  float vorticity;
  bool clear;
} pushConstants;

vec2 loadVel(ivec2 pos) {
  // TODO: Parameterize grid scale / coords
  vec2 sn = vec2(1.0);
  if (pos.x < 0 || pos.x >= pushConstants.width) {
    sn.x *= -1.0;
  }

  if (pos.y < 0 || pos.y >= pushConstants.height) {
    sn.y *= -1.0;
  }

  pos = clamp(pos, ivec2(0), ivec2(pushConstants.width - 1, pushConstants.height - 1));
  vec2 v = imageLoad(advectedVelocityField, pos).rg;
  return sn * v;
  // pos = clamp(pos, ivec2(0), ivec2(pushConstants.width - 1, pushConstants.height - 1));
  // return imageLoad(advectedVelocityField, pos).rg;
}

void main() {
  ivec2 texelPos = ivec2(gl_GlobalInvocationID.xy);
  if (texelPos.x < 0 || texelPos.x >= pushConstants.width ||
      texelPos.y < 0 || texelPos.y >= pushConstants.height) {
    return;
  }

  float h = max(1.0 / pushConstants.width, 1.0 / pushConstants.height);
  
  // Calculate local divergence  
  vec2 vel = loadVel(texelPos);
  vec2 vR = loadVel(texelPos + ivec2(1, 0));
  vec2 vL = loadVel(texelPos + ivec2(-1, 0));
  vec2 vU = loadVel(texelPos + ivec2(0, 1));
  vec2 vD = loadVel(texelPos + ivec2(0, -1));

  float div = 0.5 / h * (vR.x - vL.x + vU.y - vD.y);

  if (pushConstants.clear) {
    div = 0.0;
  }

  imageStore(divergenceField, texelPos, vec4(div, 0.0, 0.0, 1.0));
}




