
#version 450

#include "SimulationCommon.glsl"

layout(local_size_x = 16, local_size_y = 16) in;

#define pressureFieldImage _r16fimageHeap[simUniforms.pressureFieldImage]

float loadP(ivec2 pos) {
  pos.x = 
      (pos.x < simUniforms.width) ? 
        (pos.x < 0) ? 
          abs(pos.x) - 1 : 
          pos.x : 
        (2 * simUniforms.width - pos.x - 1);
  pos.y = 
      (pos.y < simUniforms.height) ? 
        (pos.y < 0) ? 
          abs(pos.y) - 1 : 
          pos.y : 
        (2 * simUniforms.height - pos.y - 1);
  // pos = clamp(pos, ivec2(0), ivec2(simUniforms.width - 1, simUniforms.height - 1));

  return imageLoad(pressureFieldImage, pos).r;  
}

void main() {
  ivec2 texelPos = ivec2(gl_GlobalInvocationID.xy);
  if (texelPos.x < 0 || texelPos.x >= simUniforms.width ||
      texelPos.y < 0 || texelPos.y >= simUniforms.height) {
    return;
  }
  
  // Project velocity field to be divergence free
  vec2 uvScale = vec2(1.0) / vec2(simUniforms.width, simUniforms.height);
  vec2 texelUv = (vec2(texelPos) + vec2(0.5)) * uvScale;

  float h = max(uvScale.x, uvScale.y);

  vec2 vel = imageLoad(advectedVelocityFieldImage, texelPos).rg;
  float pR = loadP(texelPos + ivec2(1, 0));
  float pL = loadP(texelPos + ivec2(-1, 0));
  float pU = loadP(texelPos + ivec2(0, 1));
  float pD = loadP(texelPos + ivec2(0, -1));

  vel -= 0.5 / h * vec2(pR - pL, pU - pD);

  if (isClearFlagSet()) {
    vel = vec2(0.0);
  }

  imageStore(velocityFieldImage, texelPos, vec4(vel, 0.0, 1.0));
}




